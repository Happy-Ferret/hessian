EP_TITLE        = $0000

EP_SCIENTIST1   = $0100

EP_SCIENTIST2   = $0200
EP_GARAGECOMPUTER = $0201

EP_RADIOUPPERLABSENTRANCE = $0300
EP_RADIOSECURITYCENTER = $0301

EP_CONFIGUREUPGRADE = $0400
EP_INSTALLUPGRADE = $0401
EP_INSTALLEFFECT = $0402

EP_ENTERCODE    = $0500
EP_ENTERCODELOOP = $0501

EP_ELEVATOR     = $0600
EP_ELEVATORLOOP = $0601
EP_RADIOUPPERLABSELEVATOR = $0602
EP_RADIOSECURITYPASS = $0603
EP_ESCORTSCIENTISTSREFRESH = $0604
EP_ESCORTSCIENTISTSZONE = $0605
EP_HACKERFOLLOW = $0606
EP_HACKERFOLLOWZONE = $0607

EP_USEHEALTHRECHARGER = $0700
EP_USEBATTERYRECHARGER = $0701
EP_RECHARGEREFFECT = $0702
EP_COMBATROBOTSABOTEUR = $0703
EP_DESTROYCOMBATROBOTSABOTEUR = $0704

EP_RECYCLINGSTATION = $0800
EP_CONSTRUCTSPEECH = $0801

EP_MOVEROTORDRONE = $0900
EP_DESTROYROTORDRONE = $0901
EP_HACKER       = $0902
EP_HACKER2      = $0903

EP_INSTALLAMPLIFIER = $0a00
EP_RUNLASER     = $0a01
EP_SWITCHGENERATOR = $0a02
EP_SWITCHLASER = $0a03
EP_MOVEGENERATOR = $0a04

EP_RADIOCAVES   = $0b00
EP_MOVEBAT      = $0b01
EP_MOVESPIDER   = $0b02
EP_MOVELARGESPIDER = $0b03
EP_OPENWALL     = $0b04
EP_MOVEACID     = $0b05
EP_RADIOLOWERLABS = $0b06

EP_SUBNETROUTER = $0c00
EP_SERVERROOMCOMPUTER = $0c01
EP_MOVESCIENTISTS = $0c02
EP_RADIOCONSTRUCT = $0c03

EP_HACKER3      = $0d00
EP_HACKER4      = $0d01

EP_ESCORTSCIENTISTSSTART = $0e00

EP_ESCORTSCIENTISTSFINISH = $0f00
EP_THRONECHIEF  = $0f01
EP_RADIOFINDFILTER = $0f02
EP_BEGINAMBUSH  = $0f03
EP_RADIOCONSTRUCT2 = $0f04

EP_HACKERAMBUSH = $1000
EP_GIVELAPTOP = $1001

EP_BEGINSURGERY = $1100
EP_BEGINSURGERY2 = $1101

EP_AFTERSURGERY = $1200
EP_AFTERSURGERYRUN = $1201
EP_AFTERSURGERYZONE = $1202
EP_AFTERSURGERYNOAIR = $1203
EP_AFTERSURGERYFOLLOW = $1204
EP_AFTERSURGERYNOAIRDIE = $1205
EP_AFTERSURGERYNOAIRRADIO = $1206

EP_REACHOLDTUNNELS = $1300
EP_HACKERFOLLOWFINISH = 1301
EP_ENTERLAB = $1302
EP_HACKERENTERLAB = $1303
EP_SCIENTISTENTERLAB = $1306

EP_LABCOMPUTER = $1400
EP_GIVELAPTOP2 = $1401

EP_MOVESECURITYCHIEF = $1500
EP_DESTROYSECURITYCHIEF = $1501
EP_SECURITYCHIEFSPEECH = $1502
EP_ENTERBIODOME = $1503
EP_BIODOMEENDING = $1504

EP_INSTALLLAPTOP = $1600
EP_INSTALLLAPTOPWORK = $1601
EP_INSTALLLAPTOPFINISH = $1602
EP_HACKERFINAL = $1603

EP_TUNNELMACHINE = $1700
EP_TUNNELMACHINEITEMS = $1701
EP_TUNNELMACHINERUN = $1702
EP_RADIOJORMUNGANDR = $1703
EP_RADIOJORMUNGANDRRUN = $1704
EP_DESTROYPLAN = $1705
EP_MOVELARGETANK = $1706
EP_MOVEFIREBALL = $1707

EP_HAZMAT = $1800
EP_HAZMATLEAVE = $1801
EP_DESTROYCOMMENT = $1802
EP_RADIOHACKERWARNING = $1803

EP_MOVEJORMUNGANDR = $1900

EP_MOVEEYESTAGE1 = $1a00
EP_MOVEEYESTAGE2 = $1a01
EP_DESTROYEYE = $1a02
EP_CONSTRUCTENDING = $1a03

EP_ENDING1 = $1b00
EP_ENDING2 = $1b01
EP_ENDING3 = $1b02

EP_LOBBYCOMPUTER = $1c00
EP_THEATRECOMPUTER = $1c01
EP_OFFICECOMPUTER1 = $1c02
EP_OFFICECOMPUTER2 = $1c03
EP_OFFICECOMPUTER3 = $1c04
EP_OFFICECOMPUTER4 = $1c05
EP_OFFICECOMPUTER5 = $1c06

EP_ITCOMPUTER = $1d00
EP_SECURITYCOMPUTER1 = $1d01
EP_SECURITYCOMPUTER2 = $1d02
EP_SECURITYCOMPUTER3 = $1d03
EP_SECURITYCOMPUTER4 = $1d04
EP_SECURITYCOMPUTER5 = $1d05
EP_SECURITYCOMPUTER6 = $1d06

EP_LABCOMPUTER1 = $1e00
EP_LABCOMPUTER2 = $1e01
EP_LABCOMPUTER3 = $1e02
EP_LABCOMPUTER4 = $1e03
EP_LABCOMPUTER5 = $1e04
EP_LABCOMPUTER6 = $1e05
EP_LABCOMPUTER7 = $1e06
EP_LABCOMPUTER8 = $1e07
EP_LABCOMPUTER9 = $1e08

EP_LOWERLABSCOMPUTER1 = $1f00
EP_LOWERLABSCOMPUTER2 = $1f01
EP_LOWERLABSCOMPUTER3 = $1f02
EP_LOWERLABSCOMPUTER4 = $1f03
EP_LOWERLABSCOMPUTER5 = $1f04
EP_LOWERLABSCOMPUTER6 = $1f05
EP_LOWERLABSCOMPUTER7 = $1f06
EP_LOWERLABSCOMPUTER8 = $1f07
EP_LOWERLABSCOMPUTER9 = $1f08

EP_LOWERSECURITYCOMPUTER1 = $2000
EP_LOWERSECURITYCOMPUTER2 = $2001
EP_LOWERSECURITYCOMPUTER3 = $2002
EP_LOWERSECURITYCOMPUTER4 = $2003
EP_LOWERSECURITYCOMPUTER5 = $2004

PLOT_ELEVATOR1  = $00                       ;Upper <-> lower lab
PLOT_ELEVATOR2  = $01                       ;Jormungandr <-> Bio-Dome
PLOT_GENERATOR  = $02                       ;Upper labs generator switched on
PLOT_AMPINSTALLED = $03                     ;Laser amplifier installed
PLOT_HIDEOUTOPEN = $04                      ;Rotordrone boss destroyed
PLOT_BATTERY    = $05                       ;Tunnel machine battery installed
PLOT_FUEL       = $06                       ;Tunnel machine refueled
PLOT_DISRUPTCOMMS = $07                     ;AI communication disrupted with the laptop
PLOT_LOWERLABSNOAIR = $08                   ;Air being sucked from lower labs (must be plotbit 8)
PLOT_MOVESCIENTISTS = $09                   ;Scientists moved to wait in upper labs
PLOT_ELEVATORMSG = $0a                      ;Player attempted lower lab elevator entry
PLOT_ESCORTCOMPLETE = $0b                   ;Scientists got to the operating room
PLOT_OLDTUNNELSLAB1 = $0c                   ;Linda reached old tunnels lab
PLOT_OLDTUNNELSLAB2 = $0d                   ;Jeff reached old tunnels lab
PLOT_HIDEOUTAMBUSH = $0e                    ;Robot ambush to Jeff's hideout ongoing
PLOT_RIGTUNNELMACHINE = $0f                 ;Tunnel machine prepared with explosives for simultaneous destruction

SPEECHBUBBLEOFFSET = -40*8

        ; Execute a script
        ;
        ; Parameters: A script entrypoint, X script file, ES_ParamX+1, ES_ParamY+1 (or Y in ExecScriptParam)
        ; Returns: -
        ; Modifies: A,X,Y,loader temp vars

ExecScriptParam:sty ES_ParamY+1
ExecScript:     pha
ES_LoadedScriptFile:
                cpx #$ff                        ;Check if same file already loaded
                beq ES_SameFile
                stx ES_LoadedScriptFile+1
                txa
                ldx #F_SCRIPT
                jsr MakeFileName
                lda #$00                        ;Reset any text printing in case it was from
                sta textHi                      ;script and will be overwritten
                lda #<scriptCodeStart
                ldx #>scriptCodeStart
                jsr LoadFileRetry
ES_SameFile:    pla
                bmi ES_LoadOnly
                asl
                tax
                lda scriptCodeStart,x
                sta ES_ScriptJump+1
                lda scriptCodeStart+1,x
                sta ES_ScriptJump+2
ES_ParamX:      ldx #$00
ES_ParamY:      ldy #$00
ES_ScriptJump:  jmp $1000

        ; Set/stop a continuous script
        ;
        ; Parameters: A script entrypoint, X script file (0 = stop)
        ; Returns: -
        ; Modifies: -
        
StopScript:     ldx #$00
SetScript:      stx scriptF
                sta scriptEP
ES_LoadOnly:    rts

        ; Set/stop zone transition script
        ;
        ; Parameters: A script entrypoint, X script file (0 = stop)
        ; Returns: -
        ; Modifies: -
        
StopZoneScript: ldx #$00
SetZoneScript:  stx zoneScriptF
                sta zoneScriptEP
                rts

        ; NPC speak a line
        ;
        ; Parameters: Y actor type, A,X text address
        ; Returns: -
        ; Modifies: A,X,Y,temp1-temp4

SpeakLine:      sty SL_ActT+1
                jsr PrintPanelTextIndefinite
SL_ActT:        lda #$00
                jsr FindActor
                bcc SL_NoSpeechBubble
                lda #ACTI_FIRSTEFFECT
                ldy #ACTI_LASTEFFECT
                jsr GetFreeActor
                bcc SL_NoSpeechBubble
                lda #$00
                sta temp1
                sta temp2
                lda #<SPEECHBUBBLEOFFSET
                sta temp3
                lda #>SPEECHBUBBLEOFFSET
                sta temp4
                lda #ACT_SPEECHBUBBLE
                jsr SpawnWithOffset
SL_NoSpeechBubble:
                lda #$00                        ;Reset controls during conversation
                sta actCtrl+ACTI_PLAYER
                sta actMoveCtrl+ACTI_PLAYER
                ldx #MENU_DIALOGUE
                jmp SetMenuMode

        ; Get the value of a plotbit
        ;
        ; Parameters: A plotbit number
        ; Returns: A nonzero if set
        ; Modifies: A,Y

GetPlotBit:     jsr DecodeBit
                and plotBits,y
                rts

        ; Set a plotbit
        ;
        ; Parameters: A plotbit number
        ; Returns: -
        ; Modifies: A,Y

SetPlotBit:     jsr DecodeBit
                ora plotBits,y
                bne CPB_Store

        ; Clear a plotbit
        ;
        ; Parameters: A plotbit number
        ; Returns: -
        ; Modifies: A,Y

ClearPlotBit:   jsr DecodeBit
                eor #$ff
                and plotBits,y
CPB_Store:      sta plotBits,y
                rts

        ; Turn a number into a byte offset into a bit-table and a bitmask
        ;
        ; Parameters: A number
        ; Returns: A bitmask, Y byte offset
        ; Modifies: A,Y

DecodeBit:      pha
                and #$07
                tay
                lda keyRowBit,y
                eor #$ff
                sta DB_Value+1
                pla
                lsr
                lsr
                lsr
                tay
DB_Value:       lda #$00
SameLevel:      rts
