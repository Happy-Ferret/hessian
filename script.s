EP_TITLE        = $0000

EP_USEHEALTHRECHARGER = $0100
EP_USEBATTERYRECHARGER = $0101
EP_RECHARGEREFFECT = $0102
EP_ENTERCODE    = $0103
EP_ENTERCODELOOP = $0104
EP_ELEVATOR     = $0105
EP_ELEVATORLOOP = $0106
EP_RECYCLINGSTATION = $0107
EP_RADIOUPPERLABSELEVATOR = $0108
EP_RADIOSECURITYPASS = $0109
EP_ESCORTSCIENTISTSSTART = $010a
EP_ESCORTSCIENTISTSREFRESH = $010b
EP_ESCORTSCIENTISTSZONE = $010c
EP_ESCORTSCIENTISTSFINISH = $010d
EP_HACKERFOLLOW = $010e
EP_HACKERFOLLOWZONE = $010f
EP_COMBATROBOTSABOTEUR = $0110
EP_DESTROYCOMBATROBOTSABOTEUR = $0111
EP_HACKER3      = $0112
EP_HACKER4      = $0113

EP_GAMESTART    = $0200
EP_SCIENTIST1   = $0201
EP_SCIENTIST2   = $0202
EP_RADIOUPPERLABSENTRANCE = $0203
EP_RADIOSECURITYCENTER = $0204
EP_MOVEROTORDRONE = $0205
EP_DESTROYROTORDRONE = $0206
EP_HACKER       = $0207
EP_HACKER2      = $0208
EP_INSTALLAMPLIFIER = $0209
EP_RUNLASER     = $020a
EP_SWITCHGENERATOR = $020b
EP_SWITCHLASER = $020c
EP_MOVEGENERATOR = $020d
EP_MOVELARGESPIDER = $020e
EP_OPENWALL     = $020f
EP_MOVEACID     = $0210
EP_RADIOCAVES   = $0211

EP_RADIOLOWERLABS = $0300
EP_HACKERAMBUSH = $0301
EP_GIVELAPTOP = $0302
EP_SUBNETROUTER = $0303
EP_SERVERROOMCOMPUTER = $0304
EP_MOVESCIENTISTS = $0305
EP_RADIOCONSTRUCT = $0306
EP_THRONECHIEF = $0307
EP_FINDFILTER   = $0308
EP_BEGINAMBUSH  = $0309
EP_RADIOCONSTRUCT2 = $030a
EP_BEGINSURGERY = $030b
EP_BEGINSURGERY2 = $030c
EP_AFTERSURGERY = $030d
EP_AFTERSURGERYRUN = $030e
EP_AFTERSURGERYZONE = $030f
EP_AFTERSURGERYNOAIR = $0310
EP_AFTERSURGERYFOLLOW = $0311
EP_AFTERSURGERYNOAIRDIE = $0312
EP_AFTERSURGERYNOAIRRADIO = $0313
EP_REACHOLDTUNNELS = $0314

EP_CONFIGUREUPGRADE = $0400
EP_INSTALLUPGRADE = $0401
EP_INSTALLEFFECT = $0402

EP_MOVESECURITYCHIEF = $0500
EP_DESTROYSECURITYCHIEF = $0501
EP_SECURITYCHIEFSPEECH = $0502
EP_ENTERBIODOME = $0503
EP_BIODOMEENDING = $0504
EP_INSTALLLAPTOP = $0505
EP_INSTALLLAPTOPWORK = $0506
EP_INSTALLLAPTOPFINISH = $0507
EP_HACKERFOLLOWFINISH = $0508
EP_ENTERLAB = $0509
EP_HACKERENTERLAB = $050a
EP_LABCOMPUTER = $050b
EP_GIVELAPTOP2 = $050c
EP_SCIENTISTENTERLAB = $050d
EP_HAZMAT = $050e
EP_HAZMATLEAVE = $050f
EP_DESTROYCOMMENT = $0510
EP_HACKERFINAL = $0511
EP_TUNNELMACHINE = $0512
EP_TUNNELMACHINEITEMS = $0513
EP_TUNNELMACHINERUN = $0514
EP_RADIOJORMUNGANDR = $0515
EP_RADIOJORMUNGANDRRUN = $0516
EP_DESTROYPLAN = $0517

EP_MOVEJORMUNGANDR = $0600

EP_MOVEEYESTAGE1 = $0700
EP_MOVEEYESTAGE2 = $0701
EP_DESTROYEYE = $0702
EP_CONSTRUCTSPEECH = $0703
EP_CONSTRUCTENDING = $0704
EP_ENDING1 = $0705
EP_ENDING2 = $0706
EP_ENDING3 = $0707

TEXT_WAREHOUSE1 = $0000
TEXT_WAREHOUSE2 = $0001
TEXT_PARKINGGARAGE1 = $0002
TEXT_PARKINGGARAGE2 = $0003
TEXT_PARKINGGARAGE3 = $0004
TEXT_PARKINGGARAGE4 = $0005

TEXT_ENTERUPPERLABS = $0100
TEXT_ENTERSECURITYCENTER = $0101
TEXT_ELEVATORLOCKED = $0102
TEXT_SERVICEPASS = $0103
TEXT_HACKER1 = $0104
TEXT_HACKER1PERCENT = $0105
TEXT_HACKER2 = $0106
TEXT_ELEVATORLOCKEDHASAMPLIFIER = $0107

TEXT_ENTERCAVES = $0200
TEXT_ENTERLOWERLABS = $0201
TEXT_HACKER3 = $0202
TEXT_HACKER4A = $0203
TEXT_HACKER4B = $0204

TEXT_RADIOMOVESCIENTISTS = $0300
TEXT_RADIOCONSTRUCT = $0301
TEXT_RADIOCONSTRUCT2 = $0302
TEXT_AMBUSHFAIL = $0303
TEXT_AMBUSHSUCCESS = $0304
TEXT_AMBUSHLAPTOP = $0305

TEXT_ESCORTBEGIN = $0400
TEXT_ESCORTFINISH = $0401
TEXT_RADIOFINDFILTER = $0402

TEXT_BEGINSURGERY1 = $0403
TEXT_BEGINSURGERY2 = $0404
TEXT_BEGINSURGERY3 = $0405
TEXT_AFTERSURGERY1 = $0406
TEXT_AFTERSURGERY2 = $0407
TEXT_AFTERSURGERY3 = $0408
TEXT_AFTERSURGERY4 = $0409
TEXT_NOAIR = $040a
TEXT_NOAIRDIE = $040b
TEXT_NOAIRCODE = $040c
TEXT_RADIONOAIR = $040d
TEXT_NOAIRSUCCESS = $040e

TEXT_RADIOAMBUSHFAIL = $0500
TEXT_RADIOABANDONED = $0501
TEXT_RADIOTRIGGEREND = $0502
TEXT_SECURITYCHIEF = $0503
TEXT_RADIOINSTALLLAPTOP = $0504
TEXT_RADIOINSTALLLAPTOPFINISH = $0505

TEXT_ENTEROLDTUNNELS = $0600
TEXT_ENTERLAB = $0601
TEXT_LABCOMPUTER = $0602
TEXT_GIVELAPTOP = $0603
TEXT_ENTERLAB2 = $0604
TEXT_HACKERFINAL = $0607

TEXT_RADIOJORMUNGANDR = $0700
TEXT_RADIOCONSTRUCTSPEAKS1 = $0701
TEXT_RADIOCONSTRUCTSPEAKS2 = $0702
TEXT_HAZMAT = $0703
TEXT_DESTROYCOMMENT = $0704

PLOT_ELEVATOR1  = $00                       ;Upper <-> lower lab
PLOT_ELEVATOR2  = $01                       ;Jormungandr <-> Bio-Dome
PLOT_GENERATOR  = $02                       ;Upper labs generator switched on
PLOT_AMPINSTALLED = $03                     ;Laser amplifier installed
PLOT_HIDEOUTOPEN = $04                      ;Rotordrone boss destroyed
PLOT_BATTERY    = $05                       ;Tunnel machine battery installed
PLOT_FUEL       = $06                       ;Tunnel machine refueled
PLOT_DISRUPTCOMMS = $07                     ;AI communication disrupted with the laptop
PLOT_LOWERLABSNOAIR = $08                   ;Air being sucked from lower labs (must be plotbit 8)
PLOT_MOVESCIENTISTS = $09                   ;Scientists moved to wait in upper labs
PLOT_ELEVATORMSG = $0a                      ;Player attempted lower lab elevator entry
PLOT_ESCORTCOMPLETE = $0b                   ;Scientists got to the operating room
PLOT_OLDTUNNELSLAB1 = $0c                   ;Linda reached old tunnels lab
PLOT_OLDTUNNELSLAB2 = $0d                   ;Jeff reached old tunnels lab
PLOT_HIDEOUTAMBUSH = $0e                    ;Robot ambush to Jeff's hideout ongoing
PLOT_RIGTUNNELMACHINE = $0f                 ;Tunnel machine prepared with explosives for simultaneous destruction

SPEECHBUBBLEOFFSET = -40*8

        ; Execute a script
        ;
        ; Parameters: A script entrypoint, X script file, ES_ParamX+1, ES_ParamY+1 (or Y in ExecScriptParam)
        ; Returns: -
        ; Modifies: A,X,Y,loader temp vars

ExecScriptParam:sty ES_ParamY+1
ExecScript:     pha
ES_LoadedScriptFile:
                cpx #$ff                        ;Check if same file already loaded
                beq ES_SameFile
                stx ES_LoadedScriptFile+1
                txa
                ldx #F_SCRIPT
                jsr MakeFileName
                lda #$00                        ;Reset any text printing in case it was from
                sta textHi                      ;script and will be overwritten
                lda #<scriptCodeStart
                ldx #>scriptCodeStart
                jsr LoadFileRetry
ES_SameFile:    pla
                bmi ES_LoadOnly
                asl
                tax
                lda scriptCodeStart,x
                sta ES_ScriptJump+1
                lda scriptCodeStart+1,x
                sta ES_ScriptJump+2
ES_ParamX:      ldx #$00
ES_ParamY:      ldy #$00
ES_ScriptJump:  jmp $1000

        ; Set/stop a continuous script
        ;
        ; Parameters: A script entrypoint, X script file (0 = stop)
        ; Returns: -
        ; Modifies: -
        
StopScript:     ldx #$00
SetScript:      stx scriptF
                sta scriptEP
ES_LoadOnly:    rts

        ; Set/stop zone transition script
        ;
        ; Parameters: A script entrypoint, X script file (0 = stop)
        ; Returns: -
        ; Modifies: -
        
StopZoneScript: ldx #$00
SetZoneScript:  stx zoneScriptF
                sta zoneScriptEP
                rts

        ; NPC speak a line
        ;
        ; Parameters: Y actor type, A,X text address
        ; Returns: -
        ; Modifies: A,X,Y,temp1-temp4

SpeakLine:      sty SL_ActT+1
                jsr PrintPanelTextIndefinite
SL_ActT:        lda #$00
                jsr FindActor
                bcc SL_NoSpeechBubble
                lda #ACTI_FIRSTEFFECT
                ldy #ACTI_LASTEFFECT
                jsr GetFreeActor
                bcc SL_NoSpeechBubble
                lda #$00
                sta temp1
                sta temp2
                lda #<SPEECHBUBBLEOFFSET
                sta temp3
                lda #>SPEECHBUBBLEOFFSET
                sta temp4
                lda #ACT_SPEECHBUBBLE
                jsr SpawnWithOffset
SL_NoSpeechBubble:
                lda #$00                        ;Reset controls during conversation
                sta actCtrl+ACTI_PLAYER
                sta actMoveCtrl+ACTI_PLAYER
                ldx #MENU_DIALOGUE
                jmp SetMenuMode

        ; Get the value of a plotbit
        ;
        ; Parameters: A plotbit number
        ; Returns: A nonzero if set
        ; Modifies: A,Y

GetPlotBit:     jsr DecodeBit
                and plotBits,y
                rts

        ; Set a plotbit
        ;
        ; Parameters: A plotbit number
        ; Returns: -
        ; Modifies: A,Y

SetPlotBit:     jsr DecodeBit
                ora plotBits,y
                bne CPB_Store

        ; Clear a plotbit
        ;
        ; Parameters: A plotbit number
        ; Returns: -
        ; Modifies: A,Y

ClearPlotBit:   jsr DecodeBit
                eor #$ff
                and plotBits,y
CPB_Store:      sta plotBits,y
                rts

        ; Turn a number into a byte offset into a bit-table and a bitmask
        ;
        ; Parameters: A number
        ; Returns: A bitmask, Y byte offset
        ; Modifies: A,Y

DecodeBit:      pha
                and #$07
                tay
                lda keyRowBit,y
                eor #$ff
                sta DB_Value+1
                pla
                lsr
                lsr
                lsr
                tay
DB_Value:       lda #$00
SameLevel:      rts

        ; Get text resource address (load if necessary)
        ;
        ; Parameters: A text file number, X text number
        ; Returns: A,X address
        ; Modifies: A,X,sprFileLo-Hi,frameLo

GetTextAddress: sty frameLo
                tay
                lda fileHi,y
                bne GTA_Loaded
                jsr LoadSpriteFile              ;Ok to reuse
GTA_Loaded:     sta sprFileHi
                lda fileLo,y
                sta sprFileLo
                lda #$00
                sta fileAge,y
                txa
                asl
                tay
                lda (sprFileLo),y
                pha
                iny
                lda (sprFileLo),y
                tax
                pla
                ldy frameLo
                rts
